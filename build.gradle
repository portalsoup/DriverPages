buildscript {
    repositories {
//        jcenter()
        mavenCentral()
//        maven { url "https://plugins.gradle.org/m2/" }
        maven {
                url "${artifactory_url}"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
            name = "maven-main-cache"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.14"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.8"
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.4.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"
apply plugin: "net.ltgt.apt"


sourceCompatibility = 1.8
version = '0.17'
group = 'com.jcleary'

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'gradle-dev-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-dev'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.jcleary', name: 'misc-java', version: '1.0'

    compile 'org.seleniumhq.selenium:selenium-java:2.52.0'
    compile "com.github.detro.ghostdriver:phantomjsdriver:1.1.0"
    compile 'org.testng:testng:6.9.6'
    compile 'org.hamcrest:hamcrest-all:1.3'
    compile 'org.mockito:mockito-core:1.10.19'
    compile 'org.projectlombok:lombok:1.16.14'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.slf4j:slf4j-simple:1.7.23'
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked"
    aptOptions.annotationProcessing = true
    aptOptions.processors = [
            "com.jcleary.annotations.processors.HackProcessor"
    ]
}

configurations {
    apt
}
test {
    useTestNG()

//     systemProperty 'some.prop', 'value'
//
//     explicitly include or exclude tests
//    include 'org/foo/**'
//    exclude 'org/boo/**'

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    minHeapSize = "128m"
    maxHeapSize = "512m"

    // set JVM arguments for the test JVM(s)
    jvmArgs '-XX:MaxPermSize=256m'

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}


cobertura {
    coverageFormats = ['html']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura");

}

test.finalizedBy(project.tasks.cobertura)